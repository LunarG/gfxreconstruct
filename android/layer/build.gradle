apply plugin: 'com.android.library'

android {
    compileSdkVersion 34
    namespace 'com.lunarg.gfxreconstruct.layer'
    ndkVersion '26.3.11579264'
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        ndk {
            if (providers.gradleProperty("armeabi-v7a").present) {
                abiFilters 'armeabi-v7a'
            } else if (providers.gradleProperty("arm64-v8a").present) {
                abiFilters 'arm64-v8a'
            } else if (providers.gradleProperty("x86").present) {
                abiFilters 'x86'
            } else if (providers.gradleProperty("x86_64").present) {
                abiFilters 'x86_64'
            } else {
                abiFilters 'arm64-v8a', 'x86_64'
            }
        }
        externalNativeBuild {
            cmake {
                cppFlags "-fexceptions", "-Wno-nullability-completeness"
                arguments "-DANDROID_TOOLCHAIN=clang", "-DANDROID_STL=c++_static"

                if (providers.gradleProperty("DisableOpenXR").present) {
                    logger.info('GFXReconstruct layer built with NO OpenXr Support! [DisableOpenXR found]')
                    arguments.add("-DGFXRECON_ENABLE_OPENXR=OFF")
                }
                else {
                    logger.info('GFXReconstruct layer built with OpenXr Support.')
                    arguments.add("-DGFXRECON_ENABLE_OPENXR=ON")
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    sourceSets {
       main {
           assets.srcDirs = ['assets']
       }
    }
}
