apply plugin: 'com.android.library'

android {
    compileSdkVersion 33
    namespace 'com.lunarg.gfxreconstruct.layer'
    ndkVersion '26.3.11579264'
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        ndk {
            if (project.hasProperty("armeabi-v7a")) {
                abiFilters 'armeabi-v7a'
            } else if (project.hasProperty("arm64-v8a")) {
                abiFilters 'arm64-v8a'
            } else if (project.hasProperty("x86")) {
                abiFilters 'x86'
            } else if (project.hasProperty("x86_64")) {
                abiFilters 'x86_64'
            } else {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
        externalNativeBuild {
            cmake {
                cppFlags "-fexceptions", "-std=c++17", "-Wno-nullability-completeness"
                arguments "-DANDROID_TOOLCHAIN=clang", "-DANDROID_STL=c++_static"
                if (project.hasProperty("DisableOpenXR")) {
                    logger.info('GFXReconstruct layer built with NO OpenXr Support! [DisableOpenXR found]')
                    arguments.add("-DGFXRECON_ENABLE_OPENXR=OFF")
                }
                else {
                    logger.info('GFXReconstruct layer built with OpenXr Support.')
                    arguments.add("-DGFXRECON_ENABLE_OPENXR=ON")
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    sourceSets {
       main {
           assets.srcDirs = ['assets']
       }
    }
}
